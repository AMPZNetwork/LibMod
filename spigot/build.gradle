buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.yaml:snakeyaml:+'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "de.undercouch.download" version "5.5.0"
    id 'org.comroid.codegen.spigot'
}

group = 'com.ampznetwork.libmod'

sourceSets {
    main.java.srcDirs = files("${parent.projectDir}/src/$project.name/main/java")
    main.resources.srcDirs = files("${parent.projectDir}/src/$project.name/main/resources")
    test.java.srcDirs = files("${parent.projectDir}/src/$project.name/test/java")
    test.resources.srcDirs = files("${parent.projectDir}/src/$project.name/test/java")
}

tasks.register('dlSpigotServerJar', Download).configure {
    src 'https://download.getbukkit.org/spigot/spigot-1.20.1.jar'
    dest "$projectDir/run/spigot.jar"
    outputs.upToDateWhen {
        file("$projectDir/run/spigot.jar").exists()
    }
}

tasks.register('dlPaperServerJar', Download).configure {
    src 'https://api.papermc.io/v2/projects/paper/versions/1.20.1/builds/196/downloads/paper-1.20.1-196.jar'
    dest "$projectDir/run/paper.jar"
    outputs.upToDateWhen {
        file("$projectDir/run/paper.jar").exists()
    }
}

tasks.register('dlArclightServerJar', Download).configure {
    src 'https://github.com/IzzelAliz/Arclight/releases/download/Trials%2F1.0.5/arclight-forge-1.20.1-1.0.5.jar'
    dest "$projectDir/run/arclight.jar"
    outputs.upToDateWhen {
        file("$projectDir/run/arclight.jar").exists()
    }
}

tasks.register('dlLuckPermsJar', Download).configure {
    src 'https://download.luckperms.net/1552/bukkit/loader/LuckPerms-Bukkit-5.4.137.jar'
    dest "$projectDir/run/plugins/LuckPerms.jar"
    outputs.upToDateWhen {
        file("$projectDir/run/plugins/LuckPerms.jar").exists()
    }
}

tasks.register('dlSparkJar', Download).configure {
    src 'https://ci.lucko.me/job/spark/lastSuccessfulBuild/artifact/spark-bukkit/build/libs/spark-1.10.86-bukkit.jar'
    dest "$projectDir/run/plugins/spark.jar"
    outputs.upToDateWhen {
        file("$projectDir/run/plugins/spark.jar").exists()
    }
}

shadowJar {
    // Specify which projects' classes to include
    from(project(":japi").sourceSets.main.output)
    from(project(":${prefix}:api").sourceSets.main.output)
    from(project(":${prefix}:core").sourceSets.main.output)

    // Exclude dependencies by ensuring the JAR contains only the classes
    configurations = [project.configurations.shadow]
}

tasks.register('testPlugin', Copy) {
    dependsOn shadowJar
    from shadowJar.outputs.files.singleFile
    into "${projectDir}/run/plugins"
    rename { n -> "LibMod-spigot.jar" }
}

task simplifyArtifact(type: Copy) {
    dependsOn test, shadowJar
    from shadowJar.outputs.files.singleFile
    into "$rootDir/build/libs/"
    rename { n -> "LibMod-spigot.jar" }
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    // libMod api
    implementation project("$prefix:core")

    // spigot
    implementation 'org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bungeecord:4.3.3'

    // luckperms
    compileOnly 'net.luckperms:api:5.4'
    // placeholderapi
    compileOnly 'me.clip:placeholderapi:2.+'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
